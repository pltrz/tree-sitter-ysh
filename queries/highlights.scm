((string) @string)

[
  "var"
  "setvar"
  "const"
  "setglobal"
  "call"
] @keyword

[
  "for"
  "in"
  "while"
] @keyword.repeat

[
 "if"
 "elif"
 "else"
 "case"
] @keyword.conditional

(number) @number

((comment)+ @comment)

((command_name) @function.builtin
  (#any-of? @function.builtin "echo" "type" "shopt" "json" "write" "assert" "fork" "forkwait"))
[
  "shvar"
] @function.builtin
((command_name) @keyword.import
  (#eq? @keyword.import "use"))
((command_name) "=" @keyword.debug)
((command_name) @function.call)

[
  "func"
  "proc"
  "typed"
] @keyword.function

"return" @keyword.return

[
  "!"
  "+"
  "-"
  "*"
  "/"
  "**"
  "<"
  "<>"
  ">"
  ">&"
  ">|"
  "|"
  "^"
  "&"
  ">>"
  "<<"
  "<<<"
  ">>&"
  "&>>"
  "<("
  "%"
  "<="
  ">="
  "="
  "+="
  "-="
  "*="
  "/="
  "==="
  "~=="
  "!=="
  "~"
  "!~"
  "~~"
  "!~~"
  "++"
  ":-"
  "=>"
  "."
  "->"
  ":"
  "..."
  "&&"
  "||"
  (range_operator)
] @operator

[
  "or"
  "and"
  "not"
  "is"
] @keyword.operator

(boolean) @boolean
(null) @constant.builtin

variable: (variable_name) @variable
constant: (variable_name) @constant
((variable_name) @variable)
(variable_assignment key: (variable_name) @property)

; (dollar_token) @punctuation.special

member: (variable_name) @variable.member
key: (variable_name) @variable.member

((variable_name) @variable.builtin
                 (#eq? @variable.builtin "_error"))

(function_definition (function_name) @function)
(proc_definition (proc_name) @function)
(function_call (function_name) @function.call)
(method_call method: (function_name) @function.method.call)
parameter: (variable_name) @variable.parameter
(rest_of_arguments) @variable.parameter
(parameter_list (named_parameter (variable_name) @variable.parameter))
(proc_parameter_list (named_parameter (variable_name) @variable.parameter))

[
  (escape_sequence)
  (escaped_bytes)
  (escape_special_characters)
  (escaped_newline)
  (escaped_double_quote)
  (escaped_single_quote)
  (escaped_newline_value)
] @string.escape

; (function_call) @function.call
; ((function_name) @function.builtin (#any-of? @function.builtin "echo" "cat"))

[
  "("
  ")"
  "["
  "]"
  "{"
  "}"
  "/"
] @punctuation.bracket

(literal_list [ ":|" "|" ] @punctuation.bracket)

"," @punctuation.delimiter

[
 "$"
 ";"
 "@"
] @punctuation.special

redirection_value: (word) @variable.parameter
